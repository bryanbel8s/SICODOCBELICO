
CREATE DATABASE SICODOC
GO
USE SICODOC
GO

CREATE TABLE ROL(
  IDROL INT IDENTITY(1,1) PRIMARY KEY,
  NOMBREROL NVARCHAR(100) NOT NULL UNIQUE
)
GO

CREATE TABLE USUARIO(
  RFC NVARCHAR(13) NOT NULL PRIMARY KEY
      CHECK (LEN(RFC) = 13 AND RFC COLLATE Latin1_General_CS_AS NOT LIKE '%[^A-Z0-9]%'),
  NOMBRE NVARCHAR(255) NOT NULL,
  APELLIDO NVARCHAR(255) NOT NULL,
  CORREO NVARCHAR(255) NOT NULL UNIQUE,
  CONSTRAINT CK_USUARIO_CORREO CHECK (CORREO LIKE '%@%.%'),
  CONTRASENA_HASH VARBINARY(256) NOT NULL,
  FIRMA_DIGITAL VARBINARY(MAX) NULL,
  IDROL INT NOT NULL,
  CONSTRAINT FK_USUARIO_ROL FOREIGN KEY(IDROL) REFERENCES ROL(IDROL)
)
GO

CREATE TABLE CONVOCATORIA(
  IDCONVOCATORIA INT IDENTITY(1,1) PRIMARY KEY,
  NOMBRE NVARCHAR(100) NOT NULL,
  FECHAINICIOREGISTRO DATE NOT NULL,
  FECHAFINREGISTRO DATE NOT NULL,
  FECHAINICIOEVALUACION DATE NOT NULL,
  FECHAFINEVALUACION DATE NOT NULL,
  FECHA_PUB DATE NOT NULL
)
GO

CREATE TABLE EXPEDIENTE(
  IDEXPEDIENTE INT IDENTITY(1,1) PRIMARY KEY,
  ESTADO NVARCHAR(100) NOT NULL,
  RFC_USUARIO NVARCHAR(13) NOT NULL,
  IDCONVOCATORIA INT NOT NULL,
  FECHACREACION DATETIME2 NOT NULL DEFAULT SYSUTCDATETIME(),
  CONSTRAINT FK_EXPEDIENTE_USUARIO FOREIGN KEY(RFC_USUARIO) REFERENCES USUARIO(RFC),
  CONSTRAINT FK_EXPEDIENTE_CONVOCATORIA FOREIGN KEY(IDCONVOCATORIA) REFERENCES CONVOCATORIA(IDCONVOCATORIA)
)
GO

CREATE TABLE DOCUMENTO(
  IDDOCUMENTO INT IDENTITY(1,1) PRIMARY KEY,
  TIPO NVARCHAR(100) NOT NULL,
  FECHAGENERACION DATETIME2 NOT NULL DEFAULT SYSUTCDATETIME(),
  RFC_USUARIO NVARCHAR(13) NOT NULL,
  IDEXPEDIENTE INT NOT NULL,
  IDCOMITE INT NULL,
  CONSTRAINT FK_DOCUMENTO_USUARIO FOREIGN KEY(RFC_USUARIO) REFERENCES USUARIO(RFC),
  CONSTRAINT FK_DOCUMENTO_EXPEDIENTE FOREIGN KEY(IDEXPEDIENTE) REFERENCES EXPEDIENTE(IDEXPEDIENTE)
)
GO

CREATE TABLE REVISION_DOCUMENTO (
  IDREVISION INT IDENTITY(1,1) PRIMARY KEY,
  IDDOCUMENTO INT NOT NULL,
  RFC_DIRECTOR NVARCHAR(13) NOT NULL,
  ESTADO NVARCHAR(50) DEFAULT 'En revisión' 
      CHECK (ESTADO IN ('En revisión', 'Aprobado', 'Rechazado')),
  COMENTARIOS NVARCHAR(MAX) NULL,
  FECHA_REVISION DATETIME2 DEFAULT SYSUTCDATETIME(),
  FIRMA_DIGITAL VARBINARY(MAX) NULL,
  CONSTRAINT FK_REVISION_DOCUMENTO_DOC 
      FOREIGN KEY(IDDOCUMENTO) REFERENCES DOCUMENTO(IDDOCUMENTO),
  CONSTRAINT FK_REVISION_DOCUMENTO_DIRECTOR 
      FOREIGN KEY(RFC_DIRECTOR) REFERENCES USUARIO(RFC)
)
GO

-- VISTA para los DOCENTES
CREATE VIEW v_documentos_docente AS
SELECT 
  d.IDDOCUMENTO,
  d.TIPO,
  d.FECHAGENERACION,
  d.RFC_USUARIO AS RFC_DOCENTE,
  u.NOMBRE + ' ' + u.APELLIDO AS NOMBRE_DOCENTE,
  ISNULL(r.ESTADO, 'En proceso') AS ESTADO_REVISION,
  r.RFC_DIRECTOR,
  r.FECHA_REVISION
FROM DOCUMENTO d
LEFT JOIN REVISION_DOCUMENTO r 
  ON r.IDDOCUMENTO = d.IDDOCUMENTO
JOIN USUARIO u 
  ON u.RFC = d.RFC_USUARIO
GO

-- VISTA para los DIRECTORES
CREATE VIEW v_revision_pendiente_director AS
SELECT 
  r.IDREVISION,
  d.IDDOCUMENTO,
  d.TIPO,
  u.NOMBRE + ' ' + u.APELLIDO AS DOCENTE,
  d.FECHAGENERACION,
  r.ESTADO,
  r.COMENTARIOS
FROM REVISION_DOCUMENTO r
JOIN DOCUMENTO d 
  ON d.IDDOCUMENTO = r.IDDOCUMENTO
JOIN USUARIO u 
  ON u.RFC = d.RFC_USUARIO
WHERE r.ESTADO = 'En revisión'
GO

--INDICES
CREATE INDEX IX_EXPEDIENTE_USUARIO ON EXPEDIENTE(RFC_USUARIO)
CREATE INDEX IX_EXPEDIENTE_CONVOCATORIA ON EXPEDIENTE(IDCONVOCATORIA)
CREATE INDEX IX_DOCUMENTO_EXPEDIENTE ON DOCUMENTO(IDEXPEDIENTE)

GO
